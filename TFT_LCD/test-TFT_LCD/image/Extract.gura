import(jpeg)

main() = {
	fileNameSrc = 'cat.jpg'
	imageSrc = Image(fileNameSrc).Rotate(90)
	baseNameSrc = path.BaseName(fileNameSrc)
	WriteImageHeaderFile(baseNameSrc, imageSrc.Resize(128, nil))
	WriteImageHeaderFile(baseNameSrc, imageSrc.Resize(160, nil))
	WriteImageHeaderFile(baseNameSrc, imageSrc.Resize(240, nil))
}

WriteImageHeaderFile(baseNameSrc as String, image as Image) = {
	iCol = 0
	varName = Format('image_%s_%dx%d', baseNameSrc, image.width, image.height)
	Open(varName + '.h', 'w') {|fout|
		fout.Println(e'const jxglib::Image ${varName} { jxglib::Image::Format::RGB565, ${image.width}, ${image.height},')
		image.Scan():pixel {|pixel|
			(iCol == 0) && fout.Printf('"')
			rgb565 = pixel.color.rgb565
			fout.Printf('\\x%02x\\x%02x', rgb565 & 0xff, rgb565 >> 8)
			iCol += 1
			if (iCol == 16) {
				fout.Printf('"\n')
				iCol = 0
			}
		}
		(iCol > 0) && fout.Printf('"\n')
		fout.Printf('};\n')
	}
}

main()
